%macro get_common_columns_meta(mSrcDS, mTrgDS, outputData);
    %global _RC_ _RS_;
    %local mname;
    %let mname=&sysmacroname;
    %clearerr;
    data &outputData(keep=column src_len src_fmt src_infmt src_type trg_len trg_fmt trg_infmt trg_type);
    length src_type trg_type $1 src_fmt src_infmt trg_fmt trg_infmt column $32;
    src_dsid=open("&mSrcDS", "i");
    if src_dsid<=0 then do;
        call symputx('_RC_', 1);
        call symputx('_RS_', "&sysmacroname: Unable to open &mSrcDS");
        stop;
    end;
    trg_dsid=open("&mTrgDS", "i");
    if trg_dsid<=0 then do;
        call symputx('_RC_', 1);
        call symputx('_RS_', "&sysmacroname: Unable to open &mTrgDS");
        if src_dsid>0 then rc=close(src_dsid);
        stop;
    end;
    NVARS = attrn(trg_dsid, 'NVARS');
    do trg_num=1 to NVARS;
        column=varname(trg_dsid, trg_num);
        src_num=varnum(src_dsid, column);
        if src_num<=0 then continue;
        src_type=vartype(src_dsid, src_num);
        src_fmt=varfmt(src_dsid, src_num);
        src_infmt=varinfmt(src_dsid, src_num);
        src_len=varlen(src_dsid, src_num);
        trg_type=vartype(trg_dsid, trg_num);
        trg_fmt=varfmt(trg_dsid, trg_num);
        trg_infmt=varinfmt(trg_dsid, trg_num);
        trg_len=varlen(trg_dsid, trg_num);
        output;
    end;
    if src_dsid>0 then rc=close(src_dsid);
    if trg_dsid>0 then rc=close(trg_dsid);
    run;
%mend;