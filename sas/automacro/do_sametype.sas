%macro do_sametype(mvTplDS);
%global _RC_ _RS_;
%local dsid rc;
%let mname=&sysmacroname;
%clearerr;
%let dsid=%sysfunc(open(&mvTplDS, I));
%if &dsid <= 0 %then %return;
%syscall set(dsid);
%let rc = %sysfunc(fetch(&dsid));
%do %while(&rc=0);
    %let column=&column;
    %let src_len=&src_len;
    %let src_fmt=&src_fmt;
    %let src_infmt=&src_infmt;
    %let src_type=&src_type;
    %let trg_len=&trg_len;
    %let trg_fmt=&trg_fmt;
    %let trg_infmt=&trg_infmt;
    %let trg_type=&trg_type;
    DROP &column;
    RENAME _&column._=&column;
    %if &trg_fmt ne %then %do; FORMAT _&column._ &trg_fmt; %end;
    %if &trg_infmt ne %then %do; INFORMAT _&column._ &trg_infmt; %end;
    %if &trg_type = C %then %do;
        LENGTH _&column._ $&trg_len;
        %if &src_type = N %then %do; 
            _&column._=STRIP(VVALUE(&column));
        %end;
        %else %do;
            _&column._=&column;
        %end;
    %end;
    %else %do;
        LENGTH _&column._ &trg_len;
        %if %sysfunc(prxmatch(/^(DATE)\d*\./i, &trg_fmt)) %then %do;
            %if %sysfunc(prxmatch(/^(WORDDATX)\d*\./i, &src_fmt)) %then %do;
                _&column._=&column;
            %end;
            %else %if %sysfunc(prxmatch(/^(MMDDYY)\d*\./i, &src_fmt)) %then %do;
                _&column._=&column;
            %end;
            %else %do;
                _&column._=INPUT(VVALUE(&column), anydtdte.); 
            %end;
        %end;
        %else %if %sysfunc(prxmatch(/^(DATETIME)\d*\./i, &trg_fmt)) %then %do;
            %if %sysfunc(prxmatch(/^(WORDDATX)\d*\./i, &src_fmt)) %then %do;
                _&column._=&column*86400;
            %end;
            %else %if %sysfunc(prxmatch(/^(MMDDYY)\d*\./i, &src_fmt)) %then %do;
                _&column._=&column*86400;
            %end;
            %else %do;
                _&column._=INPUT(VVALUE(&column), anydtdtm.);
            %end;
        %end;
        %else %if &src_type = C %then %do;
            _&column._=INPUT(&column, best.);
        %end;
        %else %do;
            _&column._=&column;
        %end;         
    %end;
    %let rc = %sysfunc(fetch(&dsid));
%end;
%let rc=%sysfunc(close(&dsid));
%mend do_sametype;